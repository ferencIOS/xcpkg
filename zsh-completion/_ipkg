#compdef ipkg

_commands=(
    '-h:print the help infomation of ipkg command.'
    '--help:print the help infomation of ipkg command.'
    'help:print the help infomation of ipkg command.'
    '-V:print the version of ipkg and NDK.'
    '--version:print the version of ipkg and NDK.'
    'version:print the version of ipkg and NDK.'
    'search:search packages can be installed.'
    'list:list the target platforms, target CPU arch, available, installed and outdated packages.'
    'tree:list contents of a installed package directory in a tree-like format.'
    'info:print the basic infomation of packages.'
    'rule:view, edit a rule or create a new rule.'
    'formula:view,edit the formula of a package.'
    'install:install packages.'
    'reinstall:reinstall packages.'
    'uninstall:uninstall packages.'
    'cleanup:cleanup the unused cache.'
    'update:update the formula repository.'
    'upgrade:upgrade the outdated packages.'
    'fetch:download formula resources of a package to the cache.'
    'homepage:visit the homepage of a formula or the ipkg project.'
    'prefix:show the installation direcotory of a formula or the ipkg home.'
    'pack:pack a installed package.'
    'logs:print the logs of a installed package.'
    'is:check something. easy to write script.'
    'create-framework:create framework.'
    'min-version:get the min-version of the specified platform, or all platforms.'
)

_list=(
    'rules:list the rules.'
    'versions:list the supportted versions of target platforms.'
    'platforms:list the supportted platforms of target platforms.'
    'archs:list the supportted archs of target platforms.'
    'available:list the available packages.'
    'installed:list the installed packages.'
    'outdated:list the installed packages which can be upgraded.'
    'notdepended:list the packages which is not depended by other packages'
)

function _ipkg_list() {
    _describe 'list' _list
}

function _ipkg_commands() {
    _describe 'command' _commands
}

function _ipkg_available_packages() {
    local -a _available_packages
    _available_packages=($(ipkg list available -q))
    _describe 'available-packages' _available_packages
}

function _ipkg_installed_packages() {
    local -a _installed_packages
    _installed_packages=($(ipkg list installed))
    _describe 'installed-packages' _installed_packages
}

function _ipkg_outdated_packages() {
    local -a _outdated_packages
    _outdated_packages=($(ipkg list outdated))
    _describe 'outdated-packages' _outdated_packages
}

function _ipkg() {
    local context state state_descr line
    typeset -A opt_args

    _arguments \
        '1: :_ipkg_commands' \
        '*:: :->option'

    case $state in
        option)
            case $words[1] in
                list)
                    _arguments \
                        ':list:_ipkg_list' \
                        '*:: :->list-option'
                    case $state in
                        list-option)
                            case $words[1] in
                                available)
                                    _arguments '-q[quiet mode. do not report error log]';;
                                archs|versions)
                                    _arguments ':platform:($(ipkg list platforms))';;
                                rules)
                                    _arguments '--table[show more infomation]';;
                            esac
                    esac
                    ;;
                formula)
                    _arguments \
                        ':action:(create delete rename view edit list)' \
                        '*:: :->action'
                    case $state in
                        action)
                            case $words[1] in
                                delete|rename|view|edit)
                                    _arguments ':package-name:_ipkg_available_packages'
                            esac
                    esac
                    ;;
                rule)
                    _arguments \
                        ':action:(create delete rename view edit list)' \
                        '*:: :->action'
                    case $state in
                        action)
                            case $words[1] in
                                delete|rename|view|edit)
                                    _arguments ':package-name:($(ipkg list rules))'
                            esac
                    esac
                    ;;
                tree)
                    _arguments ':package-name:_ipkg_available_packages'
                    ;;
                info)
                    _arguments '*:package-name:_ipkg_available_packages'
                    ;;
                fetch|homepage)
                    _arguments ':package-name:_ipkg_available_packages'
                    ;;
                prefix|pack|create-framework)
                    _arguments ':package-name:_ipkg_installed_packages'
                    ;;
                logs)
                    _arguments \
                        '1:package-name:_ipkg_installed_packages' \
                        '2:abi:($(ipkg list abis $words[2]))' \
                    ;;
                min-version)
                    _arguments \
                        ':platform:($(ipkg list platforms))'
                    ;;
                install)
                    _arguments \
                        '1:package-name:_ipkg_available_packages' \
                        '--rule=-[rule to install]:rule:($(ipkg list rules))' \
                        '--dry-run[dry-run]' \
                        '--keep-working-dir[do not delete working-dir even if installed success]' \
                        '(--verbose -v)'{--verbose,-v}'[verbose output log]' \
                        '(--xtrace -x)'{--xtrace,-x}'[trace execution of commands]'
                    ;;
                reinstall)
                    _arguments \
                        '1:package-name:_ipkg_installed_packages' \
                        '(--verbose -v)'{--verbose,-v}'[verbose output log]' \
                        '(--xtrace -x)'{--xtrace,-x}'[trace execution of commands]'
                    ;;
                upgrade)
                    _arguments \
                        ':package-name:_ipkg_outdated_packages' \
                        '(--verbose -v)'{--verbose,-v}'[verbose output log]' \
                        '(--xtrace -x)'{--xtrace,-x}'[trace execution of commands]'
                    ;;
                uninstall)
                    _arguments '*:package-name:_ipkg_installed_packages'
                    ;;
                is)
                    _arguments \
                        '1: :(available installed outdated)' \
                        '2:package-name:_ipkg_available_packages'
                    ;;
                *);;
            esac
            ;;
        *);;
    esac
}

_ipkg "$@"

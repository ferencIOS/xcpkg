#compdef xcpkg

_xcpkg_actions=(
    '-h:show help of this command.'
    '--help:show help of this command.'
    '-V:show version of this command.'
    '--version:show version of this command.'
    'search:search packages can be installed.'
    'ls-target-platform-names:list the supportted platforms of target platforms.'
    'ls-target-platform-versions:list the supportted versions of target platforms.'
    'ls-target-platform-archs:list the supportted archs of target platforms.'
    'ls-available:list the available packages.'
    'ls-installed:list the installed packages.'
    'ls-outdated:list the installed packages which can be upgraded.'
    'is-available:is the given package available.'
    'is-installed:is the given package installed.'
    'is-outdated:is the given package outdated.'
    'tree:list files of the given installed package in a tree-like format.'
    'info:show infomation of the given package or all available packages.'
    'formula-edit:edit the formula of the given package.'
    'formula-repo-init:create a new empty formula repository.'
    'formula-repo-add:create a new empty formula repository then sync with server.'
    'formula-repo-del:delete the given formula repository from your local cache.'
    'formula-repo-sync:sync the given formula repository.'
    'formula-repo-conf:change the config of the given formula repository.'
    'formula-repo-info:show infomation of the given formula repository.'
    'formula-repo-list:list all available formula repositories.'
    'formula:show formula of the given package.'
    'receipt:show receipt of the given package.'
    'install:install packages.'
    'reinstall:reinstall packages.'
    'uninstall:uninstall packages.'
    'cleanup:delete the unused cached files.'
    'update:update the formula repository.'
    'upgrade:upgrade the outdated packages.'
    'upgrade-self:upgrade this software.'
    'fetch:download formula resources of the given package to the cache.'
    'integrate:integrate zsh completions.'
    'sysinfo:show your system information.'
    'xcinfo:show your current actived xcode information.'
    'env:show system and current actived xcode information.'
    'depends:show depends of the given package.'
    'pack:pack all the installed files of the given installed package to a archive file.'
    'logs:show logs of the given installed package.'
    'gen-url-transform-sample:generate url-transform sample.'
)

function _xcpkg_available_packages() {
    local -a _available_packages
    _available_packages=($(xcpkg ls-available 2>/dev/null))
    _describe 'available-packages' _available_packages
}

function _xcpkg_installed_packages() {
    local -a _installed_packages
    _installed_packages=($(xcpkg ls-installed 2>/dev/null | sed 's|:|\\:|g'))
    _describe 'installed-packages' _installed_packages
}

function _xcpkg_outdated_packages() {
    local -a _outdated_packages
    _outdated_packages=($(xcpkg ls-outdated 2>/dev/null | sed 's|:|\\:|g'))
    _describe 'outdated-packages' _outdated_packages
}

function _xcpkg() {
    local context state state_descr line
    typeset -A opt_args

    _arguments \
        '1: :{_describe xcpkg_actions _xcpkg_actions}' \
        '*:: :->option'

    case $state in
        option)
            case $words[1] in
                ls-target-platform-versions)
                    _arguments ':platform:($(xcpkg ls-target-platform-names))'
                    ;;
                ls-target-platform-archs)
                    _arguments \
                        ':platform:($(xcpkg ls-target-platform-names))' \
                        ':bit:(64bit 32bit all)'
                    ;;
                formula-edit)
                    _arguments \
                        '1:package-name:_xcpkg_available_packages' \
                        '--editor=-[specify editor]'
                    ;;
                formula-repo-add)
                    _arguments \
                        '1:formula-repo-name:(name)' \
                        '2:formula-repo-url:(https://)' \
                        '--branch=-[change the formula repo branch]' \
                        + '(pin)' \
                        '--pin[set pinned to 1]' \
                        '--unpin[set pinned to 0]' \
                        + '(enable)' \
                        '--enable[set enabled to 1]' \
                        '--disable[set enabled to 0]'
                    ;;
                formula-repo-init)
                    _arguments \
                        '1:formula-repo-name:(name)' \
                        '2:formula-repo-url:(https://)' \
                        '--branch=-[change the formula repo branch]' \
                        + '(pin)' \
                        '--pin[set pinned to 1]' \
                        '--unpin[set pinned to 0]' \
                        + '(enable)' \
                        '--enable[set enabled to 1]' \
                        '--disable[set enabled to 0]'
                    ;;
                formula-repo-conf)
                    _arguments \
                        '1:formula-repo-name:($(xcpkg formula-repo-list | sed -n "/^name: /p" | cut -c7-))' \
                        '--url=-[change the formula repo url]' \
                        '--branch=-[change the formula repo branch]' \
                        + '(pin)' \
                        '--pin[set pinned to yes]' \
                        '--unpin[set pinned to no]' \
                        + '(enable)' \
                        '--enable[set enabled to yes]' \
                        '--disable[set enabled to no]'
                    ;;
                formula-repo-del)
                    _arguments '1:formula-repo-name:($(xcpkg formula-repo-list | sed -n "/^name: /p" | cut -c7-))'
                    ;;
                formula-repo-sync)
                    _arguments '1:formula-repo-name:($(xcpkg formula-repo-list | sed -n "/^name: /p" | cut -c7-))'
                    ;;
                formula-repo-info)
                    _arguments '1:formula-repo-name:($(xcpkg formula-repo-list | sed -n "/^name: /p" | cut -c7-))'
                    ;;
                formula)
                    _arguments '1:package-name:_xcpkg_available_packages' \
                        + '(op)' \
                        '--path[show formula file path only]' \
                        '--yaml[show formula file content in yaml format]' \
                        '--json[show formula file content in json format]' \
                        ':key:(summary version license web-url git-url git-sha git-ref src-url src-sha fix-url fix-sha res-url res-sha dep-pkg dep-upp dep-pym dep-plm ppflags ccflags xxflags ldflags bscript binbstd bsystem dopatch install symlink)'
                    ;;
                receipt)
                    _arguments '1:package-name:_xcpkg_installed_packages' \
                        + '(op)' \
                        '--path[show receipt file path only]' \
                        '--yaml[show receipt file content in yaml format]' \
                        '--json[show receipt file content in json format]' \
                        ':key:(summary version license web-url git-url git-sha git-ref src-url src-sha fix-url fix-sha res-url res-sha dep-pkg dep-upp dep-pym dep-plm ppflags ccflags xxflags ldflags bscript binbstd bsystem dopatch install symlink signature timestamp)'
                    ;;
                info)
                    case $words[3] in
                        installed-*|receipt-*)
                            _arguments \
                                '1:package-name:()' \
                                '2:key:(installed-dir installed-files receipt-path receipt-yaml receipt-json installed-timestamp-unix installed-timestamp-rfc-3339 installed-timestamp-iso-8601 installed-timestamp-rfc-3339-utc installed-timestamp-iso-8601-utc installed-version)'
                            ;;
                        *)  _arguments \
                                '1:package-name:(@all $(xcpkg ls-available 2>/dev/null))' \
                                '2:key:(formula-path formula-json formula-yaml summary version license web-url git-url git-sha src-url src-sha fix-url fix-sha res-url res-sha dep-pkg dep-upp dep-pym dep-plm cdefine ccflags xxflags ldflags bscript binbstd bsystem install symlink installed-dir installed-files receipt-path receipt-yaml receipt-json installed-timestamp-unix installed-timestamp-rfc-3339 installed-timestamp-rfc-3339-utc installed-timestamp-iso-8601 installed-timestamp-iso-8601-utc installed-version symlinked-files)'
                    esac
                    ;;
                fetch)
                    _arguments ':package-name:(@all $(xcpkg ls-available 2>/dev/null))'
                    ;;
                depends)
                    _arguments \
                        '1:package-name:_xcpkg_available_packages' \
                        '-t[set output format]:output-type:(dot box svg png)' \
                        '-o[set output filepath or directory]:output-path:_files'
                    ;;
                pack)
                    _arguments \
                        '1:package-name:_xcpkg_installed_packages' \
                        '-t[set output type]:output-type:(zip tar.gz tar.xz tar.lz tar.bz2)' \
                        '-o[set output filepath or directory]:output-path:_files' \
                        '--keep-working-dir[do not delete the working directory even if successfully packed]'
                    ;;
                tree)
                    _arguments \
                        '1:package-name:_xcpkg_installed_packages' \
                        '-a[show all files, including hidden ones]' \
                        '--dirsfirst[list directories before files]' \
                        '-L[level]:level:(3)'
                    ;;
                logs)
                    _arguments \
                        '1:package-name:_xcpkg_installed_packages'
                    ;;
                install)
                    _arguments \
                        ':package-name:(@all $(xcpkg ls-available 2>/dev/null))' \
                        '--toolchain=-[set which toolchain to use]:toolchain:(zig gcc llvm)' \
                        '--jobs=-[set the number of jobs you can run in parallel]:jobs:(1 2 3 4 5 6 7 8 9)' \
                        '--dry-run[dry-run]' \
                        '--keep-working-dir[do not delete the working directory even if successfully installed]' \
                        '--build-type=-[set build type]:build-type:(debug release)' \
                        '--link-type=-[link static or shared library]:link-type:(static-only shared-only static-prefered shared-prefered)' \
                        '--export-compile-commands-json[generates compile_commands.json]' \
                        '--disable-ccache[do not use ccache]' \
                        + '(verbose_level)' \
                        '-vv[debug mode. many many messages will be output to terminal]' \
                        '-v[verbose mode. many messages will be output to terminal]' \
                        '-q[silent mode. no any messages will be output to terminal]'
                    ;;
                reinstall)
                    _arguments \
                        ':package-name:_xcpkg_installed_packages' \
                        '--toolchain=-[set which toolchain to use]:toolchain:(zig gcc llvm)' \
                        '--jobs=-[set the number of jobs you can run in parallel]:jobs:(1 2 3 4 5 6 7 8 9)' \
                        '--dry-run[dry-run]' \
                        '--keep-working-dir[do not delete the working directory even if successfully installed]' \
                        '--build-type=-[set build type]:build-type:(debug release)' \
                        '--link-type=-[link static or shared library]:link-type:(static-only shared-only static-prefered shared-prefered)' \
                        '--export-compile-commands-json[generates compile_commands.json]' \
                        '--disable-ccache[do not use ccache]' \
                        + '(verbose_level)' \
                        '-vv[debug mode. many many messages will be output to terminal]' \
                        '-v[verbose mode. many messages will be output to terminal]' \
                        '-q[silent mode. no any messages will be output to terminal]'
                    ;;
                upgrade)
                    _arguments \
                        ':package-name:_xcpkg_outdated_packages' \
                        '--jobs=-[set the number of jobs you can run in parallel]:jobs:(1 2 3 4 5 6 7 8 9)' \
                        '--dry-run[dry-run]' \
                        '--keep-working-dir[do not delete the working directory even if successfully installed]' \
                        '--build-type=-[set build type]:build-type:(debug release)' \
                        '--link-type=-[link static or shared library]:link-type:(static-only shared-only static-prefered shared-prefered)' \
                        '--export-compile-commands-json[generates compile_commands.json]' \
                        '--disable-ccache[do not use ccache]' \
                        + '(verbose_level)' \
                        '-vv[debug mode. many many messages will be output to terminal]' \
                        '-v[verbose mode. many messages will be output to terminal]' \
                        '-q[silent mode. no any messages will be output to terminal]'
                    ;;
                uninstall)
                    _arguments '*:package-name:_xcpkg_installed_packages'
                    ;;
                is-available)
                    _arguments '1:package-name:_xcpkg_available_packages'
                    ;;
                is-installed)
                    _arguments '1:package-name:_xcpkg_installed_packages'
                    ;;
                is-outdated)
                    _arguments '1:package-name:_xcpkg_outdated_packages'
                    ;;
                integrate)
                    _arguments '1:what:(zsh)'
                    ;;
            esac
            ;;
        *);;
    esac
}

_xcpkg "$@"
